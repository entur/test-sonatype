plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser'
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId = project.group
            artifactId = project.name
            version = project.version

            pom {
                name = project.name
                description = "Test file"
                packaging = 'jar'

                url = "https://github.com/entur/${rootProject.name}"

                scm {
                    connection = "scm:git:https://github.com/entur/${rootProject.name}.git"
                    developerConnection = "scm:git:https://github.com/entur/${rootProject.name}.git"
                    url = "https://github.com/entur/${rootProject.name}"
                }

                licenses {
                    license {
                        name = "European Union Public Licence v. 1.2"
                        url = "https://www.eupl.eu/"
                    }
                }

                developers {
                    developer {
                        id = "reite"
                        name = "Fredrik Wilhelm Reite"
                        email = "fredrik.wilhelm.reite@entur.org"
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = project.version.endsWith("-SNAPSHOT")
                    ? "https://central.sonatype.com/repository/maven-snapshots/"
                    : layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                app {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                    snapshotSupported = true
                }
            }
        }
    }
    release {
        github {
            skipTag = true
            skipRelease = true
        }
    }
}